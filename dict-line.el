;; -*- coding: utf-8; -*-

(defgroup dict-line nil
  "Show word translation in mode line."
  :group 'convenience)

(defcustom dict-line-dict-directory "~/dict"
  "The directory of dictionary files."
  :type 'directory
  :group 'dict-line)

(defcustom dict-line-idle-delay 1
  "The number of seconds of idle time to wait before showing translation."
  :type 'number
  :group 'dict-line)

(defcustom dict-line-icon "ğŸ“š:"
  "Show icon"
  :type 'string
  :group 'dict-line)

(defcustom dict-line-more-icon " â†˜ï¸ "
  "And more Dict icon."
  :type 'string
  :group 'dict-line)

(defcustom dict-line-no-results-string "No Results "
  "No Results Dict."
  :type 'string
  :group 'dict-line)

(defcustom dict-line-dict-extension "ts"
  "Dict file extension name."
  :type 'string
  :group 'dict-line)

(defcustom dict-line-audio nil
  "Toggle Play audio file."
  :type 'boolean
  :group 'dict-line)

(defcustom dict-line-audio-root-dir "~/Dict/audio/"
  "Dict audio root dir."
  :type 'string
  :group 'dict-line)

(defcustom dict-line-audio-program "mplayer"
  "Play audio file program."
  :type 'string
  :group 'dict-line)

(defcustom dict-line-audio-file-extension ".mp3"
  "Audio file extension name."
  :type 'string
  :group 'dict-line)

(defvar dict-line--timer nil
  "Timer that is set up when the last command finished.")

(defvar dict-line--translation nil
  "The translation to be shown in the modeline.")

(defun dict-line-cancel-timer ()
  "Cancel the current idle timer."
  (when dict-line--timer
    (cancel-timer dict-line--timer)
    (setq dict-line--timer nil)))

(defun dict-line-show-translation ()
  "Show translation in the modeline."
  (unless (minibufferp)
    (let* ((word (thing-at-point 'word t))
           (translation (when (and word (not (string-empty-p word)))
                          (dict-line-get-translation word)))
           ;; å–å•è¯çš„é¦–ä¸ª å­—æ¯ ä½œä¸ºå­æ–‡ä»¶å¤¹, ä¾‹å¦‚: ~/Dict/audio/, å¦‚æœ å•è¯ä¸º time
           ;; é‚£ä¹ˆæœç´¢ ~/Dict/audio/t/ ç›®å½•
           (first-letter (when (and word (not (string-empty-p word))) ;; æ£€æŸ¥ word æ˜¯å¦å­˜åœ¨ä¸”éç©º
                           (substring (downcase word) 0 1)))
           ;; ç¡®ä¿ first-letter å’Œ word æ˜¯æœ‰æ•ˆå­—ç¬¦ä¸²
           (search-dir (when first-letter
                         (expand-file-name first-letter dict-line-audio-root-dir)))
           (file (when (and search-dir word)
                   (expand-file-name (concat word dict-line-audio-file-extension) search-dir)))
           ;; æ£€æŸ¥æ–‡ä»¶æ˜¯å¦å­˜åœ¨
           (file-exists (and file (file-exists-p file)))
           ;; æ£€æŸ¥ æ’­æ”¾éŸ³é¢‘ å¤–éƒ¨ç¨‹åº æ˜¯å¦å­˜åœ¨
           (audio-program-exists (executable-find dict-line-audio-program)))
      ;; éŸ³é¢‘ + éŸ³é¢‘æ–‡ä»¶ å­˜åœ¨
      (when (and dict-line-audio file-exists)
        (if audio-program-exists
            ;; æ’­æ”¾éŸ³é¢‘
            (call-process dict-line-audio-program nil 0 nil file)
          (message "Audio program '%s' not found." dict-line-audio-program)))
      (if translation
          (progn
            ;; ç­›æ£€, å‡å°‘ \\n å‡ºç°
            (setq dict-line--translation (concat dict-line-icon (replace-regexp-in-string "\\\\\\\\n" dict-line-more-icon translation)))
            ;; ç­›æ£€, å»è¡Œé™¤æœ«å°¾ ", ä¸­çš„ , é˜²æ­¢ä¸å…¶å®ƒ modeline å†…å®¹ç›¸è¿
            (setq dict-line--translation (replace-regexp-in-string "\"," "\" " dict-line--translation))
            ;; ç­›æ£€,  å‡å°‘ ä¸åŒè¯æ€§ \ ç¬¦å·çš„å‡ºç°
            (setq dict-line--translation (replace-regexp-in-string "\\\\" "" dict-line--translation))
            (force-mode-line-update))
        (setq dict-line--translation (concat dict-line-icon dict-line-no-results-string))
        (force-mode-line-update))
      )))

(defun dict-line-get-translation (word)
  "Get translation of the WORD from dictionary files."
  (let* ((dict-files (directory-files dict-line-dict-directory t (concat "\\." dict-line-dict-extension "$")))
         (translation nil))
    (while (and dict-files (not translation))
      (setq translation (dict-line-search-dict (car dict-files) word))
      (setq dict-files (cdr dict-files)))
    translation))

(defun dict-line-search-dict (dict-file word)
  "Search the WORD in the DICT-FILE and return its translation."
  (with-temp-buffer
    (insert-file-contents dict-file)
    (goto-char (point-min))
    (when (search-forward (concat "\"" word "\":") nil t)
      (buffer-substring-no-properties (point) (line-end-position)))))

(defun dict-line-set-timer ()
  "Set a timer to show translation when idle."
  (dict-line-cancel-timer)
  (setq dict-line--timer (run-with-idle-timer dict-line-idle-delay nil 'dict-line-show-translation)))

;;;###autoload
(define-minor-mode dict-line-mode
  "Minor mode to show word translation in mode line."
  :lighter " DictLine"
  :global nil
  (if dict-line-mode
      (progn
        (add-hook 'post-command-hook 'dict-line-set-timer nil t)
        (unless (memq 'dict-line--translation global-mode-string)
          (setq global-mode-string (append global-mode-string '("")))  ;; æ·»åŠ ä¸€ä¸ªç©ºå­—ç¬¦ä¸²
          (setq global-mode-string (append global-mode-string '(dict-line--translation)))))
    (remove-hook 'post-command-hook 'dict-line-set-timer t)
    (setq global-mode-string (remq 'dict-line--translation global-mode-string))))
;;;###autoload
(define-globalized-minor-mode global-dict-line-mode
  dict-line-mode
  (lambda () (dict-line-mode 1)))

(provide 'dict-line)
